buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.xtend:xtend-gradle-plugin:0.0.5'
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'xtend'

project.ext.mainClassName = "prof7bit.bitcoin.wallettool.Main"

repositories {
    mavenCentral()
    maven { url "https://raw.githubusercontent.com/bitcoin-solutions/mbhd-maven/master/releases" }
}

dependencies {
    compile "com.google:bitcoinj:0.11.2-MB-ALICE"
    compile 'org.slf4j:slf4j-simple:1.7.5'
    compile 'org.eclipse.xtend:org.eclipse.xtend.lib:2.5.+'
}

jar {
    from files(sourceSets.main.output.classesDir)
    from files(sourceSets.main.output.resourcesDir)    
    from {configurations.runtime.collect {it.isDirectory() ? it : zipTree(it) }} {
        // remove all signature files
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }

    manifest {
        attributes 'Main-Class': project.mainClassName
    }
}

// xtend plugin is currently broken, can't properly install
// debug info. We disable this task and restore java compiler
// destinationDir until the bug in the xtend plugin is fixed
tasks.installMainXtendDebugInfo.enabled = false
compileJava.destinationDir = sourceSets.main.output.classesDir

